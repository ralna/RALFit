***************************
* SET UP THE INITIAL DATA *
***************************

NAME          BROYDNBDLS

*   Problem :
*   *********
*   Broyden banded system of nonlinear equations, considered in the
*   least square sense.

*   Source: problem 31 in
*   J.J. More', B.S. Garbow and K.E. Hillstrom,
*   "Testing Unconstrained Optimization Software",
*   ACM Transactions on Mathematical Software, vol. 7(1), pp. 17-41, 1981.

*   See also Buckley#73 and Toint#18
*   SIF input: Ph. Toint, Dec 1989.
*   Least-squares version: Nick Gould, Oct 2015

*   classification SUR2-AN-V-0

*   N is the number of equations and variables (variable).

 IE N                   10             $-PARAMETER     original value
*IE N                   50             $-PARAMETER
*IE N                   100            $-PARAMETER
*IE N                   500            $-PARAMETER
*IE N                   1000           $-PARAMETER
*IE N                   5000           $-PARAMETER
*IE N                   10000          $-PARAMETER

*   Define some problem's parameters (see Buckley)
*   Restriction: LB + 1 + UB .le. N

 RE KAPPA1              2.0            $-PARAMETER
 RE KAPPA2              5.0            $-PARAMETER
 RE KAPPA3              1.0            $-PARAMETER
 IE LB                  5              $-PARAMETER  LB + UB + 1 .le. N
 IE UB                  1              $-PARAMETER  LB + UB + 1 .le. N

*   Define useful parameters

 IE 1                   1
 IM MLB       LB        -1
 IM MUB       UB        -1
 IA LB+1      LB        1
 I+ N-UB      N                        MUB
 IA N-UB-1    N-UB      -1
 RM -KAPPA3   KAPPA3    -1.0

VARIABLES

 DO I         1                        N
 X  X(I)
 ND

GROUPS

*   Upper left corner

 DO I         1                        LB

 IA I-1       I         -1
 IA I+1       I         1
 I+ I+UB      I                        UB

 DO J         1                        I-1
 ZN G(I)      X(J)                     -KAPPA3
 OD J

 ZN G(I)      X(I)                     KAPPA1

 DO J         I+1                      I+UB
 ZN G(I)      X(J)                     -KAPPA3

 ND

*   Main (middle) part

 DO I         LB+1                     N-UB-1

 I+ I-LB      I                        MLB
 IA I-1       I         -1
 IA I+1       I         1
 I+ I+UB      I                        UB

 DO J         I-LB                     I-1
 ZN G(I)      X(J)                     -KAPPA3
 OD J

 ZN G(I)      X(I)                     KAPPA1

 DO J         I+1                      I+UB
 ZN G(I)      X(J)                     -KAPPA3

 ND

*   Lower right corner

 DO I         N-UB                     N

 I+ I-LB      I                        MLB
 IA I-1       I         -1
 IA I+1       I         1

 DO J         I-LB                     I-1
 ZN G(I)      X(J)                     -KAPPA3
 OD J

 ZN G(I)      X(I)                     KAPPA1

 DO J         I+1                      N
 ZN G(I)      X(J)                     -KAPPA3

 ND

BOUNDS

 FR BROYDNBD  'DEFAULT'

START POINT

 XV BROYDNBD  'DEFAULT' 1.0

ELEMENT TYPE

 EV SQ        V
 EV CB        V

ELEMENT USES

 XT 'DEFAULT' CB

 DO I         1                        N
 XT E(I)      SQ
 ZV E(I)      V                        X(I)
 XT Q(I)      CB
 ZV Q(I)      V                        X(I)
 ND

GROUP TYPE

 GV L2        GVAR

GROUP USES

 T  'DEFAULT' L2

*   Upper left corner

 DO I         1                        LB

 IA I-1       I         -1
 IA I+1       I         1
 I+ I+UB      I                        UB

 DO J         1                        I-1
 ZE G(I)      E(J)                     -KAPPA3
 OD J

 ZE G(I)      Q(I)                     KAPPA2

 DO J         I+1                      I+UB
 ZE G(I)      E(J)                     -KAPPA3

 ND

*   Main (middle) part

 DO I         LB+1                     N-UB-1

 I+ I-LB      I                        MLB
 IA I-1       I         -1
 IA I+1       I         1
 I+ I+UB      I                        UB

 DO J         I-LB                     I-1
 ZE G(I)      Q(J)                     -KAPPA3
 OD J

 ZE G(I)      E(I)                     KAPPA2

 DO J         I+1                      I+UB
 ZE G(I)      E(J)                     -KAPPA3

 ND

*   Lower right corner

 DO I         N-UB                     N

 I+ I-LB      I                        MLB
 IA I-1       I         -1
 IA I+1       I         1

 DO J         I-LB                     I-1
 ZE G(I)      E(J)                     -KAPPA3
 OD J

 ZE G(I)      Q(I)                     KAPPA2

 DO J         I+1                      N
 ZE G(I)      E(J)                     -KAPPA3

 ND

OBJECT BOUND

*   Solution

*LO SOLTN               0.0

ENDATA

***********************
* SET UP THE FUNCTION *
* AND RANGE ROUTINES  *
***********************

ELEMENTS      BROYDNBDLS

INDIVIDUALS

 T  SQ
 F                      V * V
 G  V                   V + V
 H  V         V         2.0

 T  CB
 F                      V * V * V
 G  V                   3.0 * V * V
 H  V         V         6.0 * V

ENDATA

*********************
* SET UP THE GROUPS *
* ROUTINE           *
*********************

GROUPS        BROYDNBDLS

INDIVIDUALS

 T  L2
 F                      GVAR * GVAR
 G                      GVAR + GVAR
 H                      2.0

ENDATA
