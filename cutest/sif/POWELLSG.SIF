***************************
* SET UP THE INITIAL DATA *
***************************

NAME          POWELLSG

*   Problem :
*   *********

*   The extended Powell singular problem.
*   This problem is a sum of n/4 sets of four terms, each of which is
*   assigned its own group.

*   Source:  Problem 13 in
*   J.J. More', B.S. Garbow and K.E. Hillstrom,
*   "Testing Unconstrained Optimization Software",
*   ACM Transactions on Mathematical Software, vol. 7(1), pp. 17-41, 1981.

*   See also Toint#19, Buckley#34 (p.85)

*   SIF input: Ph. Toint, Dec 1989.

*   classification OUR2-AN-V-0

*   N is the number of free variables, and should be a multiple of 4

 IE N                   4              $-PARAMETER     original value
*IE N                   8              $-PARAMETER
*IE N                   16             $-PARAMETER
*IE N                   20             $-PARAMETER
*IE N                   36             $-PARAMETER
*IE N                   40             $-PARAMETER
*IE N                   60             $-PARAMETER
*IE N                   80             $-PARAMETER
*IE N                   100            $-PARAMETER
*IE N                   500            $-PARAMETER
*IE N                   1000           $-PARAMETER
*IE N                   5000           $-PARAMETER
*IE N                   10000          $-PARAMETER
*IE N                   50000          $-PARAMETER

*   Define useful parameters

 IE 1                   1
 IE 4                   4

VARIABLES

 DO I         1                        N
 X  X(I)
 ND

GROUPS

 DO I         1                        N
 DI I         4

 IA I+1       I         1
 IA I+2       I         2
 IA I+3       I         3

 XN G(I)      X(I)      1.0            X(I+1)    10.0

 XN G(I+1)    X(I+2)    1.0            X(I+3)    -1.0
 XN G(I+1)    'SCALE'   0.2

 XN G(I+2)    X(I+1)    1.0            X(I+2)    -2.0

 XN G(I+3)    X(I)      1.0            X(I+3)    -1.0
 XN G(I+3)    'SCALE'   0.1

 ND

BOUNDS

 FR POWELLSG  'DEFAULT'

START POINT

 DO I         1                        N
 DI I         4

 IA I+1       I         1
 IA I+2       I         2
 IA I+3       I         3

 X  POWELLSG  X(I)      3.0
 X  POWELLSG  X(I+1)    -1.0
 X  POWELLSG  X(I+2)    0.0
 X  POWELLSG  X(I+3)    1.0

 ND

GROUP TYPE

 GV L2        GVAR
 GV L4        GVAR

GROUP USES

 DO I         1                        N
 DI I         4

 IA I+1       I         1
 IA I+2       I         2
 IA I+3       I         3

 XT G(I)      L2
 XT G(I+1)    L2
 XT G(I+2)    L4
 XT G(I+3)    L4

 ND

OBJECT BOUND

 LO POWELLSG            0.0

*   Solution

*LO SOLTN               0.0

ENDATA

*********************
* SET UP THE GROUPS *
* ROUTINE           *
*********************

GROUPS        POWELLSG

INDIVIDUALS

 T  L2
 F                      GVAR * GVAR
 G                      GVAR + GVAR
 H                      2.0

 T  L4
 F                      GVAR**4
 G                      4.0 * GVAR**3
 H                      12.0 * GVAR**2

ENDATA
