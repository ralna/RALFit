***************************
* SET UP THE INITIAL DATA *
***************************

NAME          JENSMP

*   Problem :
*   *********

*   The Jennrich and Sampson problem

*   Source:  Problem 6 in
*   J.J. More', B.S. Garbow and K.E. Hillstrom,
*   "Testing Unconstrained Optimization Software",
*   ACM Transactions on Mathematical Software, vol. 7(1), pp. 17-41, 1981.

*   SIF input: Ph. Toint, Dec 1989.

*   classification OUR2-AN-2-0

*   This function  is a nonlinear least squares with m groups.  Each
*   group has two nonlinear elements.

*   Number of groups

 IE M                   10

*   Useful parameters

 IE 1                   1

VARIABLES

    X1
    X2

GROUPS

 DO I         1                        M
 XN G(I)
 ND

CONSTANTS

 DO I         1                        M
 IA I+1       I         1
 IM T         I+1       2
 RI 2+2I      T
 Z  JENSMP    G(I)                     2+2I
 ND

BOUNDS

 FR JENSMP    'DEFAULT'

START POINT

*   Starting from ( 0.3, 0.4 )

    JENSMP    X1        0.3
    JENSMP    X2        0.4

ELEMENT TYPE

 EV PEXP      V
 EP PEXP      P

ELEMENT USES

 XT 'DEFAULT' PEXP

 DO I         1                        M
 RI REALI     I
 ZV A(I)      V                        X1
 ZP A(I)      P                        REALI
 ZV B(I)      V                        X2
 ZP B(I)      P                        REALI
 ND

GROUP TYPE

 GV L2        GVAR

GROUP USES

 DO I         1                        M
 XT G(I)      L2
 XE G(I)      A(I)                     B(I)
 ND

OBJECT BOUND

*   Least square problems are bounded below by zero

 LO JENSMP              0.0

*  Solution

*LO SOLTN               124.362

ENDATA

***********************
* SET UP THE FUNCTION *
* AND RANGE ROUTINES  *
***********************

ELEMENTS      JENSMP

TEMPORARIES

 R  T
 R  T1
 M  EXP

INDIVIDUALS

*   Parametrized exponentials

 T  PEXP
 A  T                   EXP( P * V )
 A  T1                  P * T
 F                      T
 G  V                   T1
 H  V         V         P * T1

ENDATA

*********************
* SET UP THE GROUPS *
* ROUTINE           *
*********************

GROUPS        JENSMP

INDIVIDUALS

 T  L2
 F                      GVAR * GVAR
 G                      GVAR + GVAR
 H                      2.0

ENDATA
